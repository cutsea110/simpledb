#!/usr/bin/awk -f

function to_json(record) {

    sub("%", "", record["cache-hit-ratio"]);

    fm = sprintf("{\"read\":%d,\"written\":%d}",
		 record["read-blocks"],
		 record["written-blocks"]);
    cache = sprintf("{\"hit\":%d,\"assigned\":%d,\"ratio\":%f}",
		    record["cache-hit"],
		    record["new-assigned"],
		    record["cache-hit-ratio"]);
    bm = sprintf("{\"available\":%d,\"pinned\":%d,\"unpinned\":%d,\"cache\":%s}",
		 record["available-buffers"],
		 record["pinned-buffers"],
		 record["unpinned-buffers"],
		 cache);
    item = sprintf("{\"file-manager\":%s,\"buffer-manager\":%s,\"elapsed-time\":%f}",
		   fm,
		   bm,
		   record["elapsed-time"]);

    return item;
}

BEGIN {
    FS="[/ ()]"
}

/block size:/ {
    config["block-size"] = $(NF);
}
/num of buffer:/ {
    config["buffer-size"] = $(NF);
}
/buffer manager:/ {
    config["buffer-manager"] = $(NF);
}
/query planner:/ {
    config["query-planner"] = $(NF);
}

/numbers of read\/written blocks:/ {
    record["read-blocks"] = $(NF-1);
    record["written-blocks"] = $(NF);
}
/numbers of available buffers:/ {
    record["available-buffers"] = $(NF);
}
/numbers of total pinned\/unpinned buffers:/ {
    record["pinned-buffers"] = $(NF-1);
    record["unpinned-buffers"] = $(NF);
}
/buffer cache hit\/assigned\(ratio\):/ {
    record["cache-hit"] = $(NF-3);
    record["new-assigned"] = $(NF-2);
    record["cache-hit-ratio"] = $(NF-1);
}
/elapsed time\(secs\):/ {
    record["elapsed-time"] = $(NF);

    # itemize and add it to summary array
    summary[c++] = to_json(record);
}
END {
    record["elapsed-time"] = 0.00;
    summary[c++] = to_json(record);

    cfg = sprintf("{\"block-size\":%d,\"buffer-size\":%d,\"buffer-manager\":\"%s\",\"query-planner\":\"%s\"}",
           config["block-size"],
           config["buffer-size"],
           config["buffer-manager"],
           config["query-planner"]);

    for (i = 0; i < c; i++) {
	if (i == 0) {
	    recs = summary[i];
	} else {
	    recs = recs "," summary[i];
	}
    }

    printf("{\"config\":%s,\"records\":[%s]}", cfg, recs);
}
